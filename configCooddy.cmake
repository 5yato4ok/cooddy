message(STATUS "ConfigCooddy at " "${CUR_DIR}")
file(GLOB files "${CUR_DIR}/*")

set(shaFile "${BUILD_DIR}/.cooddy/self-check.sha1.json")
set(MY_JSON_STRING "{}")
get_filename_component(absoluteShaPath "${shaFile}" ABSOLUTE)
file(LOCK "${absoluteShaPath}.lock")
if (EXISTS "${absoluteShaPath}")
    file(READ "${absoluteShaPath}" MY_JSON_STRING)
endif ()
if ("${MY_JSON_STRING}" STREQUAL "")
    set(MY_JSON_STRING "{}")
endif ()
foreach (file ${files})
    if (NOT IS_DIRECTORY "${file}")
        file(SHA1 "${file}" fileSha)
        get_filename_component(fileName "${file}" NAME)
        string(JSON MY_JSON_STRING SET "${MY_JSON_STRING}" "${fileName}" \"${fileSha}\")
    endif ()
endforeach ()
file(WRITE "${absoluteShaPath}" "${MY_JSON_STRING}")
file(LOCK "${absoluteShaPath}.lock" RELEASE)
file(COPY ${files} DESTINATION "${BUILD_DIR}/.cooddy")
